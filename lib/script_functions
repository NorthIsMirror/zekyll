# Helper functions
pinfo() { print "${fg_bold[green]}$*$reset_color"; }
pinfon() { print -n "${fg_bold[green]}$*$reset_color"; }
pinfo2() { print "${fg_bold[yellow]}$*$reset_color"; }
pinfo2n() { print -n "${fg_bold[yellow]}$*$reset_color"; }
perror() { print "${fg_bold[red]}$*$reset_color"; }
perrorn() { print -n "${fg_bold[red]}$*$reset_color"; }
clearinput() { repeat 10; do read -rs -k 1 -t; done; }

#
# Standard outputs
#

dry-mode-msg() { [ "$ZKL_PERFORM_WRITE" = "0" ] && pinfo2 "= Dry mode =" || perror "= Actual mode (not-dry) ="; }
help-opts(){ pinfo "Options are:"; }
help-cd()  { print -- "-c/--cd             -- ask for working directory, otherwise it's current directory"; }
help-dry() { print -- "-w/--write          -- actual operation mode - else it is always dry-run mode"; }
help-git() { print -- "-g/--git            -- perform operations with git (to update repository not just files)"; }
help-path(){ print -- "-p/--path [path]    -- work in given path (don't ask for it)"; }
help-zkl() { print -- "-z/--zkl [zekylls]  -- process given zekylls"; }

#
# Detects duplicates in given array
#

detect_duplicates() {
    local -a sorted
    sorted=( "${(on)@}" )

    integer dups=0

    local prev="" i
    for i in "${sorted[@]}"; do
        if [ "$prev" = "$i" ]; then
            echo "Duplicate zekyll found: $i"
            dups=1
        fi
        prev="$i"
    done

    return $dups
}

#
# Read destination path
#

read_destination_path() {
    local zekylls_path="$1"

    while (( 1 )); do
        pinfo "Where are zekylls located? (default: current directory \".\"):"

        # TODO
        local compcontext="user:Dirs:(\"$HOME\" \"$ZKL_HOME\" \"$ZKL_DIR\")"
        vared -cp "Enter path: " zekylls_path

        if [ -z "$zekylls_path" ]; then
            zekylls_path="."
        fi

        if ! test -d "$zekylls_path"; then
            perror "Path doesn't exist, try again"
            continue
        fi

        break
    done

    REPLY="$zekylls_path"
    return 0
}

#
# Resolves path according to various rules and sources
#
resolve_path() {
    [[ -n "$ZEKYLL_DEFAULT_REPO" && -z "$ZKL_PATH" ]] && { get_path_from_repo "$ZEKYLL_DEFAULT_REPO"; ZKL_PATH="$REPLY"; }
    read_destination_path "$ZKL_PATH" || exit 1
}

#
# CD to path and show files there
#
cd_will_work_on() {
    cd "$1"

    print
    pinfo2 "Will work on following files:"
    print

    local -a listing
    listing=( "${(@f)"$( ls -1 | pr -3 -e8 -f -l 1000 -t)"}" )
    listing=( "${listing[@]:#*$'\C-L'*}" )
    print -rl -- "${listing[@]}"

    print
}

#
# Returns physicall path for given repo string
#
get_path_from_repo() {
    local user="${1%/*}"
    local repo="${1#*/}"

    # No repo given? Use "zkl"
    if [ "${1//\//}" = "$1" ]; then
        repo="zkl"
    fi

    REPLY="$ZKL_REPOS_DIR/${user}---${repo}"
}

#
# Returns physicall path for given user and repo
#
get_path_from_user_repo() {
    [ "$2" = "" ] && 2="zkl"
    REPLY="$ZKL_REPOS_DIR/${1}---${2}"
}

# vim:ft=zsh
