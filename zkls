#!/usr/bin/env zsh

ZERO="${(%):-%N}"
REPO_DIR="${ZERO%/*}"
# On asciinema 0 and ZERO are functionargzero
[ "$ZERO" = "$REPO_DIR" ] && REPO_DIR="$HOME/.zekyll/zekyll"
source "$REPO_DIR/lib/script_preamble"
source "$REPO_DIR/lib/script_vars"
source "$REPO_DIR/lib/script_functions"
source "$REPO_DIR/lib/math_functions"

#
# Zparseopts
#

ZKL_HELP="0"
ZKL_PATH=""
ZKL_ZEKYLLS=""
ZKL_DEFAULT="0"
ZKL_VERBOSE="-s"
ZKL_NOANSI="0"
ZKL_CODE=""
ZKL_REPO=""
ZKL_QUIET="0"
zparseopts -E -D -A opthash h -help p: -path: z: -zkl: d -default \
            v -verbose n -noansi r: -repo: q -quiet || exit 1

(( ${+opthash[-h]} + ${+opthash[--help]} )) && ZKL_HELP="1"
(( ${+opthash[-p]} )) && ZKL_PATH="${opthash[-p]}"
(( ${+opthash[--path]} )) && ZKL_PATH="${opthash[--path]}"
(( ${+opthash[-z]} )) && ZKL_ZEKYLLS="${opthash[-z]}"
(( ${+opthash[--zkl]} )) && ZKL_ZEKYLLS="${opthash[--zkl]}"
(( ${+opthash[-d]} + ${+opthash[--default]} )) && ZKL_DEFAULT="1"
(( ${+opthash[-v]} + ${+opthash[--verbose]} )) && ZKL_VERBOSE=""
(( ${+opthash[-n]} + ${+opthash[--noansi]} )) && ZKL_NOANSI="1"
(( ${+opthash[-r]} )) && ZKL_REPO="${opthash[-r]}"
(( ${+opthash[--repo]} )) && ZKL_REPO="${opthash[--repo]}"
(( ${+opthash[-q]} + ${+opthash[--quiet]} )) && ZKL_QUIET="1"

noansi

if [ "$ZKL_HELP" = "1" ]; then
    pinfo "Usage: zkl-ls [options] [path]"
    print "Asks for zekylls, and lists associated files in the same order as found in zekylls"
    print
    help-opts
    help-path
    help-dft

    exit 0
fi

#
# Code given?
#

if [ -n "$1" ]; then
    ZKL_CODE="$1"
    if [[ "$ZKL_CODE" != [0-9]##/[a-z0-9]## ]]; then
        qerror "Improper zcode, exiting"
        exit 61
    fi
fi

#
# Read destination path
# Much of pure control code
#

if [ -n "$ZKL_REPO" ]; then
    local bkp_zkl_path="$ZKL_PATH"
    if resolve_repo; then
        ZKL_PATH="$REPLY"
    else
        qerror "Provided repository doesn't exist (path: $REPLY)"
        # Next try with path if user gave it, or when user gave
        # -d and ZEKYLL_DEFAULT_REPO is not empty
        if [[ -n "$bkp_zkl_path" ]]; then
            ZKL_PATH="$bkp_zkl_path"
            qerror "Trying with provided path: $ZKL_PATH"
        elif [[ "$ZKL_DEFAULT" = "1" && -n "$ZEKYLL_DEFAULT_REPO" ]]; then
            qerror "Trying with ZEKYLL_DEFAULT_REPO: $ZEKYLL_DEFAULT_REPO"
        else
            exit 27
        fi
    fi
fi

REPLY="$ZKL_PATH"
[ "$REPLY" = "" ] && resolve_path
ZKL_PATH="$REPLY"
cd_will_work_on "$REPLY" "$ZKL_VERBOSE" || exit 1

#
# If code was given, establish ZKL_ZEKYLLS basing
# on it, overwriting any user given -z/--zkl string
#

if [ -n "$ZKL_CODE" ]; then
    if [[ "$ZKL_CODE" = (#b)([0-9]##)/([a-z0-9]##) ]]; then
        integer index="${match[1]}"
        local code="${match[2]}"
        ZKL_INDEX="$index"

        set_index "$ZKL_INDEX"
        get_zekylls_for_code "$ZKL_CODE"
        ZKL_ZEKYLLS="$REPLY"
    fi
fi

#
# Read and examine new zekylls string
#

local zekylls_string_vis=""

if [ "$ZKL_ZEKYLLS" = "" ]; then
    vared -cp "Enter zekylls: " zekylls_string_vis
else
    zekylls_string_vis="$ZKL_ZEKYLLS"
fi

print
zekylls_string="${zekylls_string_vis//_/}"
pinfo2 "Read: $zekylls_string"
print

local -a zekylls1
zekylls1=( ${(@s:_:)${zekylls_string//(#b)([a-zA-Z0-9][a-zA-Z0-9][a-zA-Z0-9])/$match[1]_}} )

pinfo "Checking current zekyll for duplicates..."
if ! detect_duplicates "${zekylls1[@]}"; then
    perror "Duplicates found. Continuing to listing the zekylls"
fi

print

#
# Skip incomplete zekyll
#

if [ "${#${zekylls1[-1]}}" = "1" ]; then
    zekylls1[-1]=()
fi

#
# List with section order
#

filter_for_existing_zekylls "${zekylls1[@]}"
local -a existing
existing=( "${reply[@]}" )
three_to_section_order "${#reply}" "${reply[@]}" "${reply2[@]}" "${reply3[@]}"

local file
for file in "${reply2[@]}"; do
    pinfo2 "-> $file"
done

print

# Handle vim specifically, adding its -p option
if [ "${EDITOR:-vim}" = "vim" ]; then
    local vimopt=" -p"
fi

echo "Your EDITOR string would be: ${EDITOR:-vim}$vimopt" $^existing"*"
echo "Other possibility is: zkl-view-zekylls -p \"$ZKL_PATH\" --zkl $zekylls_string"
