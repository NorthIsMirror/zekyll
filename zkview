#!/usr/bin/env zsh

ZERO="${(%):-%N}"
REPO_DIR="${ZERO%/*}"
# On asciinema 0 and ZERO are functionargzero
[ "$ZERO" = "$REPO_DIR" ] && REPO_DIR="$HOME/.zekyll/zekyll"
source "$REPO_DIR/lib/script_preamble"
source "$REPO_DIR/lib/script_vars"
source "$REPO_DIR/lib/script_functions"
source "$REPO_DIR/lib/math_functions"
source "$REPO_DIR/lib/codes_huffman"
source "$REPO_DIR/lib/coding_functions"

#
# Zparseopts
#

ZKL_HELP="0"
ZKL_PATH=""
ZKL_ZEKYLLS=""
ZKL_DEFAULT="0"
ZKL_VERBOSE="-s"
ZKL_NOANSI="0"
ZKL_QUIET="0"
ZKL_REPO=""
ZKL_CODE=""
ZKL_METADATA="0"
zparseopts -E -D -A opthash h -help p: -path: z: -zkl: d -default v -verbose \
            n -noansi q -quiet r: -repo: m -meta || exit 1

(( ${+opthash[-h]} + ${+opthash[--help]} )) && ZKL_HELP="1"
(( ${+opthash[-p]} )) && ZKL_PATH="${opthash[-p]}"
(( ${+opthash[--path]} )) && ZKL_PATH="${opthash[--path]}"
(( ${+opthash[-z]} )) && ZKL_ZEKYLLS="${opthash[-z]}"
(( ${+opthash[--zkl]} )) && ZKL_ZEKYLLS="${opthash[--zkl]}"
(( ${+opthash[-d]} + ${+opthash[--default]} )) && ZKL_DEFAULT="1"
(( ${+opthash[-v]} + ${+opthash[--verbose]} )) && ZKL_VERBOSE=""
(( ${+opthash[-n]} + ${+opthash[--noansi]} )) && ZKL_NOANSI="1"
(( ${+opthash[-q]} + ${+opthash[--quiet]} )) && ZKL_QUIET="1"
(( ${+opthash[-r]} )) && ZKL_REPO="${opthash[-r]}"
(( ${+opthash[--repo]} )) && ZKL_REPO="${opthash[--repo]}"
(( ${+opthash[-m]} + ${+opthash[--meta]} )) && ZKL_METADATA="1"

noansi

if [ "$ZKL_HELP" = "1" ]; then
    pinfo "Usage: zkl-view-zekylls [options]"
    print "Asks for zekylls, and displays selection list with them, launching \$EDITOR at each selection"
    print
    help-opts
    help-path
    help-zkl
    help-dft

    exit 0
fi

#
# Code given?
#

if [ -n "$1" ]; then
    ZKL_CODE="$1"
    if [[ "$ZKL_CODE" != [0-9]##/[a-z0-9]## ]]; then
        qerror "Improper zcode, exiting"
        exit 1
    fi
fi

#
# Read destination path
# Much of pure control code
#

if [ -n "$ZKL_REPO" ]; then
    local bkp_zkl_path="$ZKL_PATH"
    if resolve_repo; then
        ZKL_PATH="$REPLY"
    else
        qerror "Provided repository doesn't exist (path: $REPLY)"
        # Next try with path if user gave it, or when user gave
        # -d and ZEKYLL_DEFAULT_REPO is not empty
        if [[ -n "$bkp_zkl_path" ]]; then
            ZKL_PATH="$bkp_zkl_path"
            qerror "Trying with provided path: $ZKL_PATH"
        elif [[ "$ZKL_DEFAULT" = "1" && -n "$ZEKYLL_DEFAULT_REPO" ]]; then
            qerror "Trying with ZEKYLL_DEFAULT_REPO: $ZEKYLL_DEFAULT_REPO"
        else
            exit 1
        fi
    fi
fi

REPLY="$ZKL_PATH"
[ "$REPLY" = "" ] && resolve_path
ZKL_PATH="$REPLY"
cd_will_work_on "$REPLY" "$ZKL_VERBOSE" || exit 1

#
# If code was given, establish ZKL_ZEKYLLS basing
# on it, overwriting any user given -z/--zkl string
#

local -A mdata
if [ -n "$ZKL_CODE" ]; then
    if [[ "$ZKL_CODE" = (#b)([0-9]##)/([a-z0-9]##) ]]; then
        integer index="${match[1]}"
        local code="${match[2]}"
        ZKL_INDEX="$index"

        set_index "$ZKL_INDEX"
        get_zekylls_for_code "$ZKL_CODE"
        ZKL_ZEKYLLS="$REPLY"

        if [ "$ZKL_METADATA" = "1" ]; then
            mdata=( "${reply[@]}" )
            [ -n "${mdata[rev]}" ] && print "Meta-data/rev: ${mdata[rev]}"
            [ -n "${mdata[file]}" ] && print "Meta-data/file: ${mdata[file]}"
            [ -n "${mdata[repo]}" ] && print "Meta-data/repo: ${mdata[repo]}"
            [ -n "${mdata[site]}" ] && print "Meta-data/site: ${mdata[site]}"
        fi
    fi
fi

#
# Read and examine new zekylls string
#

local zekylls_string_vis=""

if [ "$ZKL_ZEKYLLS" = "" ]; then
    vared -cp "Enter zekylls: " zekylls_string_vis
else
    zekylls_string_vis="$ZKL_ZEKYLLS"
fi

qprint
zekylls_string="${zekylls_string_vis//_/}"
qinfo2 "Read: $zekylls_string"
qprint

local -a zekylls1
zekylls1=( ${(@s:_:)${zekylls_string//(#b)([a-zA-Z0-9][a-zA-Z0-9][a-zA-Z0-9])/$match[1]_}} )

qinfo "Checking current zekyll for duplicates..."
if ! detect_duplicates "${zekylls1[@]}"; then
    qerror "Duplicates found, but continuing to listing the zekylls"
fi

qprint

#
# Skip incomplete zekyll
#

if [[ "${#${zekylls1[-1]}}" = "1" || "${#${zekylls1[-1]}}" = "2" ]]; then
    zekylls1[-1]=()
fi

#
# Obtain section ordered list
#

# Works on given list of zekylls. Returns 3 lists
# in reply, reply2 and reply3: zekylls, file names
# and sections
filter_for_existing_zekylls "${zekylls1[@]}"

[[ "${#reply}" -eq 0 ]] && qprint && exit 0

# Zekylls, file names and sections
three_to_section_order "${#reply}" "${reply[@]}" "${reply2[@]}" "${reply3[@]}"

local -a files
files=( "${reply2[@]}" )

local file
for file in "${files[@]}"; do
    qinfo2 "-> $file"
done

#
# Display list of zekylls and open editor for any selection
#

export ZEKYLL_ZCODE="$ZKL_CODE"
export ZEKYLL_REV="${mdata[rev]}"
export ZEKYLL_FILE="${mdata[file]}"
export ZEKYLL_REPO="${mdata[repo]}"
export ZEKYLL_SITE="${mdata[site]}"
"$REPO_DIR/zsh-select-zekyll" "${files[@]}"
