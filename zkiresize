#!/usr/bin/env zsh

ZERO="${(%):-%N}"
REPO_DIR="${ZERO%/*}"
# On asciinema 0 and ZERO are functionargzero
[ "$ZERO" = "$REPO_DIR" ] && REPO_DIR="$HOME/.zekyll/zekyll"
source "$REPO_DIR/lib/script_preamble"
source "$REPO_DIR/lib/script_vars"
source "$REPO_DIR/lib/script_functions"
source "$REPO_DIR/lib/math_functions"

#
# Zparseopts
#

ZKL_PERFORM_WRITE="0"
ZKL_HELP="0"
ZKL_PATH=""
ZKL_DEFAULT="0"
ZKL_INDEX="1"
ZKL_SIZE=""
ZKL_VERBOSE="-s"
ZKL_QUIET="0"
ZKL_NOANSI="0"
ZKL_DESCRIPTION="[]"
ZKL_SECTION=""
zparseopts -E -D -A opthash h -help q -quiet p: -path: d -default i: -index: s: -size: v -verbose w -write n -noansi -section: -desc: || exit 1

(( ${+opthash[-w]} + ${+opthash[--write]} )) && ZKL_PERFORM_WRITE="1"
(( ${+opthash[-h]} + ${+opthash[--help]} )) && ZKL_HELP="1"
(( ${+opthash[-p]} )) && ZKL_PATH="${opthash[-p]}"
(( ${+opthash[--path]} )) && ZKL_PATH="${opthash[--path]}"
(( ${+opthash[-i]} )) && ZKL_INDEX="${opthash[-i]}"
(( ${+opthash[--index]} )) && ZKL_INDEX="${opthash[--index]}"
(( ${+opthash[-s]} )) && ZKL_SIZE="${opthash[-s]}"
(( ${+opthash[--size]} )) && ZKL_SIZE="${opthash[--size]}"
(( ${+opthash[-d]} + ${+opthash[--default]} )) && ZKL_DEFAULT="1"
(( ${+opthash[-v]} + ${+opthash[--verbose]} )) && ZKL_VERBOSE=""
(( ${+opthash[-q]} + ${+opthash[--quiet]} )) && ZKL_QUIET="1"
(( ${+opthash[-n]} + ${+opthash[--noansi]} )) && ZKL_NOANSI="1"
(( ${+opthash[--desc]} )) && ZKL_DESCRIPTION="${opthash[--desc]}"
(( ${+opthash[--section]} )) && ZKL_SECTION="${opthash[--section]}"

noansi

if [ "$ZKL_HELP" = "1" ]; then
    pinfo "Usage: zkiresize [options]"
    print "Resizes given index (-i) to given size (-s)"
    print
    help-opts
    help-index
    help-rsize

    exit 0
fi

quiet || dry-mode-msg

#
# Input data verification
#

if [[ "$ZKL_SIZE" -lt "0" ]]; then
    qerror "Negative index size given, aborting"
    exit 2
fi

if [[ "$ZKL_SIZE" -gt "150" ]]; then
    qerror "Maximum index size is 150, aborting"
    exit 3
fi

if [[ -n "$ZKL_SECTION" && "$ZKL_SECTION" != [A-Z] ]]; then
    qerror "Improper section given, it must be single letter A-Z"
    exit 9
fi

if [[ "$ZKL_DESCRIPTION" != "[]" && "$ZKL_DESCRIPTION" != [[:blank:]a-zA-Z0-9_-]# ]]; then
    qerror "Improper description given. Allowed are letters, digits, underscores, spaces and hyphens"
    exit 10
fi

#
# Read destination path
#

REPLY="$ZKL_PATH"
[ "$REPLY" = "" ] && resolve_path
ZKL_PATH="$REPLY"
cd_will_work_on "$REPLY" "$ZKL_VERBOSE" || exit 4

#
# Work on the index
#

# Sets ZKL_INDEX_ZEKYLLS array. It is used by the index
# subsystem as reference of what files are potentially
# part of current index
set_index "$ZKL_INDEX"

# Check
if compute_index_size_check_consistency "$ZKL_INDEX"; then
    qinfo2 "Index of size $REPLY is consistent"
    qprint
else
    qerror "Inconsistent index (of size $REPLY)! The problematic file is:" "${reply[@]}"
    qerror "It is not in sequence with other files."
    exit 5
fi

integer size="$REPLY"
local -a files
files=( "${reply[@]}" )

if [ "$ZKL_SIZE" = "" ]; then
    qinfo "You didn't request resize. Here are files of index $ZKL_INDEX, bye:"
    print -rl -- "${files[@]}"
    exit 6
fi

if [[ "$ZKL_SIZE" -eq "$size" ]]; then
    qprint "Current index size and requested index size are the same, exiting"
    exit 7
fi

if [[ "$ZKL_SIZE" -gt "$size" ]]; then
    qinfo "Requested index is greater, will create zekylls"

    # What zekylls need to be created?
    local -a new_zekylls
    integer lim="$ZKL_SIZE"
    new_zekylls=( "${(@)ZKL_INDEX_ZEKYLLS[size+1, lim]}" )
    if [ "$ZKL_PERFORM_WRITE" = "1" ]; then
        qinfon "Will create zekylls: "
    else
        qinfon "Would create zekylls (dry mode): "
    fi
    qprint ${new_zekylls[@]}
    qprint

    local write
    [ "$ZKL_PERFORM_WRITE" = "1" ] && write="-w"

    local -a sec desc
    [ -n "$ZKL_SECTION" ] && sec=( "--section" "$ZKL_SECTION" )
    [ "$ZKL_DESCRIPTION" != "[]" ] && desc=( "--desc" "$ZKL_DESCRIPTION" )

    zkcreate -p "$ZKL_PATH" $write -z "${(j::)new_zekylls}" "${sec[@]}" "${desc[@]}"
else
    qinfo "Requested index is smaller, zekylls will be moved to \"_\"-beginning file names"

    # What zekylls need to be deleted?
    local -a kept_zekylls del_zekylls
    integer lim="$ZKL_SIZE"
    kept_zekylls=( "${(@)files[1,lim]}" )
    del_zekylls=( "${(@)files:|kept_zekylls}" )

    [[ "$ZKL_SIZE" -eq 0 ]] && perror "Will delete whole index"

    if [[ "$(( quiet ))" = "1" && "$ZKL_PERFORM_WRITE" = "1" ]]; then
        pinfo "Will delete following zekylls, are you sure? [y/n]"
        print
        print -rl -- "${del_zekylls[@]}"
        print

        local key
        read -qs key

        if [ "$key" = "n" ]; then
            perror "No agreement to continue, exiting"
            exit 8
        fi
    fi

    local f
    for f in "${del_zekylls[@]}"; do
        qprint "mv ${ZKLCOL[info2]}$f${ZKLCOL[rst]} _$f"
        if [ "$ZKL_PERFORM_WRITE" = "1" ]; then
            command mv "$f" "_$f"
        fi
    done
fi
